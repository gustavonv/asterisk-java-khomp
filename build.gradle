buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    id 'nebula.nebula-release' version '4.0.1'
    id 'nebula.dependency-lock' version '4.2.0'
    id 'nebula.dependency-recommender' version '3.1.0'
}

group 'com.github.kennedyoliveira'
version '0.1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'build-dashboard'

dependencyRecommendations {
    propertiesFile file: file('dependencies_version.properties')
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}

tasks.withType(FindBugs) {
    reports {
        html.enabled = true
        xml.enabled = false
    }
}

tasks.withType(Pmd) {
    reports {
        html.enabled = true
        xml.enabled = false
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = 'ISO-8859-1'

javadoc {
    options.encoding = 'ISO-8859-1'
    title = "Asterisk Java Khomp Module (${project.version})"
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'org.slf4j:slf4j-api'
    compile 'org.slf4j:slf4j-simple'
    compile group: 'org.asteriskjava', name: 'asterisk-java'

    testCompile group: 'junit', name: 'junit'
}

jar {
    manifest {
        attributes(
                "Implementation-Title": "Asterisk Java Khomp Module",
                "Implementation-Version": version
        )
    }
}

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

//
// Release
//

modifyPom {
    project {
        name 'Asterisk Java Khomp Module'
        description 'Extension for Asterisk Java to interact with Khomp Devices.'
        url 'https://github.com/kennedyoliveira/asterisk-java-khomp'
        inceptionYear '2015'

        scm {
            url 'https://github.com/kennedyoliveira/asterisk-java-khomp'
            connection 'scm:https://kennedyoliveira@github.com/kennedyoliveira/asterisk-java-khomp.git'
            developerConnection 'scm:git://github.com/kennedyoliveira/asterisk-java-khomp.git'
        }

        licenses {
            license {
                name 'MIT License'
                url 'https://opensource.org/licenses/MIT'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'kennedyoliveira'
                name 'Kennedy Oliveira'
                email 'kennedy.oliveira@outlook.com'
                roles {
                    role 'administrator'
                    role 'developer'
                    role 'owner'
                }
            }
        }
    }
}

nexus {
    sign = true
}